// This program works, and is interesting for timing
// but the tree implementation is quite bad
// like, def doesn't work as intended
// unclear how it's still correctly working for the memoization fun
// I guess any memoization is good, even an unbalanced / slow / maybe incorrect tree

// note: does not support deletion, only add
// note: not correctly counting the height after rebalancing
class Node {
  init(key, value) {
    this.key = key;
    this.value = value;
    this.left = nil;
    this.right = nil;
    this.height = 0;
  }

  stringlevel(l) {
    if (l == this.height) {
      return "(" + this.key + ", " + this.value + ")"; 
    } else {
      var s = "";
      if (this.left) { s = s + this.left.stringlevel(l) + "  "; }
      if (this.right) { s = s + "  " + this.right.stringlevel(l); }
      return s;
    }
  }

  s() {
    for (var i = this.height; i >= 0; i = i-1) {
      print this.stringlevel(i);
    }
  }

  add(key, value) {
    if (key < this.key) {
      // add left 
      if (this.left) {
        this.left.add(key, value);
      } else {
        this.left = Node(key, value);
      }
    } else {
      // add right
      if (this.right) {
        this.right.add(key, value);
      } else {
        this.right = Node(key, value);
      }
    }
    this.reheight();
    this.rebalance();
  }

  balance() {
    return (this.right and this.right.height or -1) - (this.left and this.left.height or -1);
  }

  rebalance() {
    var b = this.balance();
    // four cases:
    // right, left, rightleft, and leftright
    if (b > 1) { // right unblanced, need some kind of left rotation
      if (this.right.balance() > 0) {
        this.rotate_left(); 
        print "left rotated";
      } else {
        this.rightleft();
        print "rightleft";
      }
    }
    if (b < -1) { // left unbalanced, need some kind of right rotation
      if (this.left.balance() < 0) {
        this.rotate_right();
        print "right rotated";
      } else {
        this.leftright();
        print "leftright";
      }
    }
    if (this.left) { this.left.rebalance(); }
    if (this.right) { this.right.rebalance(); }
  }

  // rotating by moving values around, modifying this node, and creating new nodes
  // that way, we don't have to track or modify the parent
  // I think these rotations are broken
  rotate_left() {
    var oldLeft = this.left;
    this.left = Node(this.key, this.value);
    this.left.left = oldLeft;
    // this takes on the value and right child of its right child
    this.key = this.right.key;
    this.value = this.right.value;
    this.right = this.right.right;
  }

  rotate_right() {
    var oldRight = this.right;
    this.right = Node(this.key, this.value);
    this.right.right = oldRight;
    // this takes on the value and left child of its left child
    this.key = this.left.key;
    this.value = this.left.value;
    this.left = this.left.left;
  }

  leftright() {
    this.left.rotate_left();
    this.rotate_right();
  }

  rightleft() {
    this.right.rotate_right();
    this.rotate_left();
  }

  // this should be recursive, I think
  // height for any node is max(left.height || -1, right.height || -1) + 1
  reheight() {
   if (this.left) { this.left.reheight(); }
   if (this.right) { this.right.reheight(); }

   var lh = this.left and this.left.height or -1;
   var rh = this.right and this.right.height or -1;

   if(lh > rh) {
     this.height = lh + 1;
   } else {
     this.height = rh + 1; // if they're equal, rh is as good as left
   }
  }

  get(key) {
    if (this.key == key) {
      return this.value;
    } else {
      if (key < this.key and this.left) {
        return this.left.get(key);
      } else {
        if (this.right) {
          return this.right.get(key);
        }
      }
    }
  }
}

class Results {
  init() {
    this.root = nil;
  }

  store(key, value) {
    if (this.root == nil) {
      this.root = Node(key, value);
    } else {
      this.root.add(key, value);   
    }
  }

  get(key) {
    if (this.root) {
      return this.root.get(key);
    }
  }

  s() {
    if (this.root) {
      this.root.s();
    } else {
      print "<empty>";
    }
  }
}

// umm, so we have to build a dictionary data structure for this to work
// so, I guess let's do that
fun memoize(f) {
  var results = Results();
  fun memoized(arg) {
    var stored = results.get(arg);
    if (stored) {
      return stored;
    } else {
      var calculated = f(arg);   
      results.store(arg, calculated);
      return calculated;
    }
  }
  return memoized;
}

fun fib(n) {
  if (n < 2) {
    return n;
  } else {
    return fib(n-1) + fib(n-2);
  }
}

var results = Results();
fun memoized_fib(n) {
  var stored = results.get(n);
  if (stored) {
    return stored;
  }
  var calculated;
  if (n < 2) {
    calculated = n;
  } else {
    calculated = memoized_fib(n-1) + memoized_fib(n-2);
  }
  results.store(n, calculated);
  return calculated;
}

var num = 30;

fun test_unmemoized() {
  print "unmemoized, fib of";
  print num;
  print fib(num);
}

fun test_memoized() {
  print "memoized, fib of";
  print num;
  print memoized_fib(num);
  results.s();
}

fun time(f) {
  var start = clock();
  f();
  var end = clock();
  print "calling function took ";
  print (end - start);
}

time(test_unmemoized);
time(test_memoized);
